apply plugin: 'com.android.library'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 103
        versionName "1.0.3"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant("release") {
            // if you don't want sources/javadoc, remove these lines
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api 'com.googlecode.libphonenumber:libphonenumber:8.12.54'
    api 'androidx.appcompat:appcompat:1.0.0'
}

ext {
    groupId = 'net.usamir'
    versionId = '1.0.0'
    gitProjectUrl = "https://github.com/rimoto/IntlPhoneInput"
    gitUrl = "https://github.com/rimoto/IntlPhoneInput.git"
}

group = groupId  // Maven Group ID for the artifact
version = versionId

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
afterEvaluate {
    publishing {
        publications {
            mavenRelease(MavenPublication) {
                from components.release

                groupId = groupId
                artifactId = "intlphoneinput"
                version = versionId
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                /** Configure path of your package repository on Github
                 ** Replace GITHUB_USERID with your/organisation Github userID
                 ** and REPOSITORY with the repository name on GitHub
                 */
                url = uri("https://maven.pkg.github.com/usamir/IntlPhoneInput")
                credentials {
                    /** Create github.properties in root project folder file with
                     ** gpr.usr=GITHUB_USER_ID & gpr.key=PERSONAL_ACCESS_TOKEN
                     ** Set env variable GPR_USER & GPR_API_KEY if not adding a properties file**/

                    username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }
    }
}
